plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id("com.google.android.gms.oss-licenses-plugin")
}

android {
    namespace 'com.fivegmag.a5gmsmediasessionhandler'
    compileSdk 34

    defaultConfig {
        applicationId "com.fivegmag.a5gmsmediasessionhandler"
        minSdk 29
        targetSdk 34
        versionCode 1
        versionName "1.2.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildFeatures {
        buildConfig = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.8.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation("com.google.android.gms:play-services-oss-licenses:17.0.1")
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    implementation 'org.greenrobot:eventbus:3.3.1'

    // 5GMAG
    implementation 'com.fivegmag:a5gmscommonlibrary:1.2.1'

    // Retrofit
    def retrofit_version = "2.9.0"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
}

task CaptureLibraryVersion {
    def libDef = project.configurations.getByName('implementation').allDependencies.matching {
        it.group.equals("com.fivegmag")
    }
    if (libDef.size() > 0) {
        android.buildTypes.each {
            libDef.forEach { item ->
                it.buildConfigField 'String', "LIB_VERSION_" + item.name, "\"${item.version}\""
            }
        }
    }
}